// 应用插件
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// 版本号，下次更新是只需要更改版本号即可
version = ext.VERSION
//发布到组织名称名字，必须填写
group = GROUP
//上面配置后上传至JCenter后的编译路径是这样的： compile 'com.zly.judy:judy-api:1.0.0'

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//上传到JCenter所需要的源码文件
artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories {
        mavenInstaller {
            // This generates POM.xml with proper paramters
            pom.project {

                //添加项目描述
                name 'Android 组件化库插件'
                url SITE_URL

                //设置开源证书信息
                licenses {
                    license {
                        //开源协议
                        name 'MIT License'
                        url 'https://opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        //开发者的个人信息
                        name 'Zhuliya'
                        email 'walkudjudy@gmail.com'
                    }
                }
                scm {
                    connection GIT_URL
                    developerConnection GIT_URL
                    url SITE_URL
                }
            }
        }

    }
}

//上传到JCenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

//配置上传Bintray相关信息
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        //用户组织
        userOrg = USER_ORG
        //这里的repo值必须要和你创建Maven仓库的时候的名字一样
        repo = REPO
        //发布到JCenter上的项目名字
        name = 'judy-bridge-plugin'
        //项目描述
        desc = 'Android is a simple, low-cost modular library plug-in.' // 项目描述
        websiteUrl = SITE_URL
        vcsUrl = GIT_URL
        labels = ['judy', 'module', 'bridge']
        licenses = ["MIT"]
        publish = true
    }
}

javadoc {
    options {
        //如果你的项目里面有中文注释的话，必须将格式设置为UTF-8，不然会出现乱码
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}