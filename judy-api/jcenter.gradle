apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 版本号，下次更新是只需要更改版本号即可
version = ext.VERSION
//发布到组织名称名字，必须填写
group = GROUP
//上面配置后上传至JCenter后的编译路径是这样的： compile 'com.walkud.judy:judy-api:1.0.0'

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//生成Javadoc文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到JCenter所需要的源码文件
artifacts {
    archives sourcesJar
    archives javadocJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'jar'
                //项目描述，随意填
                name 'Android 组件化库'
                url SITE_URL
                licenses {
                    license {
                        //开源协议
                        name 'MIT License'
                        url 'https://opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        //开发者的个人信息
                        id 'Walkud'
                        name 'Walkud'
                        email 'walkudlove@gmail.com'
                    }
                }
                scm {
                    connection GIT_URL
                    developerConnection GIT_URL
                    url SITE_URL
                }
            }
        }
    }
}

//上传到JCenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        //用户组织
        userOrg = USER_ORG
        //这里的repo值必须要和你创建Maven仓库的时候的名字一样
        repo = REPO
        //发布到JCenter上的项目名字
        name = 'judy-bridge'
        //项目描述
        desc = 'Android is a simple, low-cost library of modularity'
        websiteUrl = SITE_URL
        vcsUrl = GIT_URL
        labels = ['judy', 'module', 'bridge']
        licenses = ["MIT"]
        publish = true
    }
}

javadoc {
    options {
        //如果你的项目里面有中文注释的话，必须将格式设置为UTF-8，不然会出现乱码
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}